/*
 * TimeLimit Copyright <C> 2019 - 2021 Jonas Lochmann
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation version 3 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "io.timelimit.android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 151
        versionName "4.8.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
            javacOptions {
                option("-Xmaxerrs", 500)
            }
        }
        buildConfigField 'String', 'googlePlayKey', '"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAqxGPPhjSGrSI4jfIJmw83dDGTO4Sg74nXhOoHl5Omyvxa1Hv+Tzp/wC97emXSmKq9Li2bV4q3yJLjtLJtoDmh9V1vpdyjH13qOidhIUYOtlkGexbwKz56jZwRFXGBIy0RFDDr3j5DLfUWMk0dqplLgIHDqsdUNY50419oMGKAOCX3DEw+ky+0zMY/hlUkZHnd0HLi0CYNkdZyMMYnk8OiOZ3YoDU/vz9PTf93MCAONYcXBlo3Y5hdVY2tyH4qNZh1eyqe0Trz4fRf+y1ljoa0s4ZqYskbsgpy+zUbGb82cxmATAsrdJMnNKMEa30HBvfMzKiOPR6sq3A8PPPoWT5KQIDAQAB"'
        buildConfigField 'String', 'backdoorPublicKey', '"30819f300d06092a864886f70d010101050003818d0030818902818100bbb0f6029315d9a2344cac25b3af1ae751447eee6308f160d68969704b6cb4674e93ad462b13a166596d0d436a546fafb78ad79fabe158eb9b17aad4945a3cf74a6a2f771e5c836547f1ce47ce866eb2b994e5984b1e3c7457da133dde02bd0d575f588cd5bcca3acbf77c30f76bde4b16911da89caafe532c2f64b4f3f1d44b0203010001"'
        buildConfigField 'String', 'updateServer', '"https://update.timelimit.io/update.json"'
        buildConfigField 'String', 'updateServerDomain', '"update.timelimit.io"'
        // get with unzip -p *.apk META-INF/CERT.RSA | keytool -printcert | grep SHA256 | sed -e 's/\s*SHA256: //' -e 's/://g'
        // set to empty string to disable this check
        buildConfigField 'String', 'updateServerBuildsCertificate', '"2170068198547F15129DE8CC46E2A8BF1485A6E5030916E26DDE41AE0E81DAC2"'
    }

    flavorDimensions 'api', 'channel', 'server'
    productFlavors {
        storeVersion {
            dimension 'channel'
            buildConfigField 'boolean', 'storeCompilant', 'true'
            applicationIdSuffix ".store"
        }
        directVersion {
            dimension 'channel'
            buildConfigField 'boolean', 'storeCompilant', 'false'
            applicationIdSuffix ".direct"
        }

        googleApi {
            dimension 'api'
            applicationIdSuffix ".google"
        }
        noGoogleApi {
            dimension 'api'
            applicationIdSuffix ".aosp"
        }

        developmentServer {
            dimension 'server'
            // this is your own computer when using the Android Emulator
            buildConfigField 'String', 'serverUrl', '"http://10.0.2.2:8080"'
            buildConfigField 'boolean', 'hasServer', 'true'
            applicationIdSuffix '.developmentserver'
        }
        productionServer {
            dimension 'server'
            buildConfigField 'String', 'serverUrl', '"https://api.timelimit.io"'
            buildConfigField 'String', 'serverDomain', '"api.timelimit.io"'
            buildConfigField 'String', 'backupServerDomain', '"api.timelimitapp.de"'
            buildConfigField 'boolean', 'hasServer', 'true'
            // no applicationIdSuffix
        }
        noServer {
            dimension 'server'
            buildConfigField 'String', 'serverUrl', '"https://0.0.0.0"'
            buildConfigField 'boolean', 'hasServer', 'false'
            applicationIdSuffix '.offline'
        }
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    variantFilter { variant ->
        def dim = variant.flavors.collectEntries {
            [(it.productFlavor.dimension): it.productFlavor.name]
        }

        if (dim.channel == 'storeVersion' && dim.api == 'noGoogleApi') {
            variant.setIgnore(true)
        }

        if (dim.channel == 'directVersion' && dim.api == 'googleApi') {
            variant.setIgnore(true)
        }

        if (dim.server == 'noServer' && dim.api == 'googleApi') {
            variant.setIgnore(true)
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        baseline file("lint-baseline.xml")
        checkReleaseBuilds false
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    def nav_version = "2.2.1"
    def room_version = "2.2.6"
    def work_version = '2.3.4'
    def paging_version = "2.1.2"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation "com.google.android.material:material:1.3.0"
    implementation 'androidx.fragment:fragment-ktx:1.3.1'

    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation "androidx.paging:paging-runtime:$paging_version"

    implementation "android.arch.work:work-runtime:$work_version"
    implementation "android.arch.work:work-runtime-ktx:$work_version"
    androidTestImplementation "android.arch.work:work-testing:$work_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    implementation 'org.mindrot:jbcrypt:0.4'

    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'

    implementation 'com.google.android:flexbox:1.0.0'

    implementation 'com.jaredrummler:android-device-names:1.1.7'

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp-tls:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    googleApiImplementation "com.android.billingclient:billing:3.0.3"
    googleApiImplementation "com.android.billingclient:billing-ktx:3.0.3"

    implementation('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.json', module: 'json'
    }

    implementation 'org.apache.commons:commons-text:1.6'

    implementation 'org.whispersystems:curve25519-java:0.5.0'

    implementation 'com.google.zxing:core:3.2.1'
}
